( Core words from ANS Forth. )
( Copyright 2012 Corbin Simpson )
( Available under the MIT/X11 license. Ask for details. )

: */ ( n d m -- n' ) -rot * swap / ;
: /mod ( n d -- n' m ) 2dup mod -rot / ;
: 1+ ( n -- n' ) 1 + ;
: 1- ( n -- n' ) 1 - ;
: 0> ( n -- b ) 0 > ;

( decimal and hex are in the goforth core. )
: octal ( n -- ) 8 base ! ;

: aligned ( addr -- addr ) ;
: cells ( n -- n ) ;
: align ( -- ) ;

: constant ( x "<spaces>name" -- )
    create
    docol ,
    ' lit ,
    , \ Write the constant to the thread.
    ' exit ,
;
: variable ( "<spaces>name" -- )
    1 allot         \ We don't need to use cells, so we don't.
    here            \ Get here on the stack.
    create
    docol ,
    ' lit ,
    ,               \ Write here, the allocated location, onto the thread.
    ' exit ,
;

\ XXX literal mask
: id. ( addr -- )
    1+ dup @ 31 and
    begin dup 0>
    while swap 1+ dup @ emit swap 1-
    repeat
    2drop
;

\ XXX literal mask
: ?hidden ( addr -- b ) 1+ @ 32 and ;
: ?immediate ( addr -- b ) 1+ @ 128 and ;

: words ( -- )
    latest @
    begin dup 0<>
    while dup ?hidden not
        if dup id. then
    space @
    repeat
    drop cr
;

\ All done, leaving now.
here dump-core bye
